/*
 * Copyright 2018 Alibaba Group.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
bintray {
    user = property('user')
    key = property('key')
    publish = true
    override = true
    publications = ['maven_android']
    pkg {
        repo = 'maven'
        name = 'alpha'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/alibaba/alpha'

        version {
            name = '1.0.0.0'
            desc = 'A startup frameword for android app.'
            released  = new Date()
            vcsTag = '1.0.0.0'
        }
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}


publishing {
    publications {
        maven_android(MavenPublication) {
            groupId "com.alibaba.android"
            version "1.0.0.0"
            artifactId "alpha"
            artifact(sourcesJar)
            artifact("$buildDir/outputs/aar/alpha-release.aar")


            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    if (it.group != null && it.group != 'unspecified' && it.name != null && it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // Unit testing dependencies
    //testCompile 'junit:junit:4.12'
//    // Set this dependency if you want to use Mockito
//    testCompile 'org.mockito:mockito-core:1.10.19'
//    // Set this dependency if you want to use Hamcrest matching
//    androidTestCompile 'org.hamcrest:hamcrest-library:1.1'
}
